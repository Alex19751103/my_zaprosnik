

&НаСервере
Процедура ОбработатьTXTНаСервере(ФайлВХранилище)
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(ФайлВХранилище);
	ИмяТемпФайла = ПолучитьИмяВременногоФайла();
	ДвоичныеДанные.Записать(ИмяТемпФайла); 
	
	ТекстовыйФайл = Новый ТекстовыйДокумент;
	ТекстовыйФайл.Прочитать(ИмяТемпФайла);
	Т = ТекстовыйФайл.ПолучитьТекст();
	
	Если Найти(Т,"""#type"": ""jv8:ValueListType"",") > 0 ИЛИ Найти(Т,"""#type"": ""jv8:ValueTable"",") > 0 Тогда
		ОбъектОбработка = РеквизитФормыВЗначение("Объект");
		ТЗ = ОбъектОбработка.ДесерилизаторJSON(Т);
	Иначе
		ТЗ = ЗначениеИзФайла(ИмяТемпФайла);
	КонецЕсли;
	
	Если ТипЗнч(ТЗ) = Тип("СписокЗначений") Тогда
		М = ТЗ.ВыгрузитьЗначения();
		ТЗ = Новый ТаблицаЗначений;
		ТЗ.Колонки.Добавить("Значения");
		Для каждого стр Из М Цикл
			ТЗ.Добавить();
		КонецЦикла; 
		ТЗ.ЗагрузитьКолонку(М,"Значения");
	КонецЕсли;
	Колонки = ТЗ.Колонки;
	ИмяСл = Колонки.Найти("СлужебныйРек__");
	Если ИмяСл <> Неопределено  Тогда
		Колонки.Удалить("СлужебныйРек__");
	КонецЕсли;
	
	Файл = Новый Файл(ИмяТемпФайла);
	
	Если Файл.Существует() Тогда
		Попытка
			УдалитьФайлы(ИмяТемпФайла);
		Исключение
		КонецПопытки;
		
	КонецЕсли;
	
	ВывестиРезультат(ТЗ);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТЗНаСервере()
	
	ИмяТемпФайла = ПолучитьИмяВременногоФайла();
	ТЗ = ЭтаФорма[РекФормы].Выгрузить();
	
	Колонки = ТЗ.Колонки;
	ИмяСл = Колонки.Найти("СлужебныйРек__");
	Если ИмяСл <> Неопределено  Тогда
		Колонки.Удалить("СлужебныйРек__");
	КонецЕсли;
	
	//ЗначениеВФайл(ИмяТемпФайла,ТЗ);
	ОбъектОбработка = РеквизитФормыВЗначение("Объект");
	Т = ОбъектОбработка.СериализаторJSON(ТЗ);
	ТекстовыйФайл = Новый ТекстовыйДокумент;
	ТекстовыйФайл.УстановитьТекст(Т);
	ТекстовыйФайл.Записать(ИмяТемпФайла);
	ДвоичныеДанныеВложения = Новый ДвоичныеДанные(ИмяТемпФайла);
	Адрес = ПоместитьВоВременноеХранилище(ДвоичныеДанныеВложения);
	
	Файл = Новый Файл(ИмяТемпФайла);
	Если Файл.Существует() Тогда
		Попытка
			УдалитьФайлы(ИмяТемпФайла);
		Исключение
		КонецПопытки;
		
	КонецЕсли;
	
	Возврат Адрес;
	
КонецФункции

&НаСервере
Функция ИмяВФормате1С(Имя)
	
	НовоеИмя = "";
	Имя = СокрЛП(Имя);
	
	Цифры = "0123456789";
	Латиница = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz";
	Кирилица = "АБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯабвгдеёжзийклмнопрстуфхцчшщъыьэюя_";
	Все = Цифры + Латиница + Кирилица;
	
	Для сч = 1 По СтрДлина(Имя) Цикл
		Буква = Сред(Имя,сч,1);
		Если Найти(Все,Буква) > 0   Тогда
			НовоеИмя = НовоеИмя + Буква;
		Иначе
			НовоеИмя = НовоеИмя + КодСимвола(Буква);
		КонецЕсли;
	КонецЦикла;
	
	Если Найти(Цифры,Лев(НовоеИмя,1)) > 0   Тогда
		НовоеИмя = "_" + НовоеИмя;	
	КонецЕсли;
	
	НовоеИмя = СтрЗаменить(НовоеИмя,Символы.НПП,"");
	
	Возврат НовоеИмя;
	
КонецФункции // ()

&НаСервере
Процедура ЗагрузитьИзФайлаXLSНаСервере(Данные,КолСтрок,КолСтолбцов);
	
	ТЗ = Новый ТаблицаЗначений;
	
	Для Сч=0 По КолСтолбцов-1 Цикл
		Имя = ИмяВФормате1С(Данные[Сч][0]);
		Попытка
			ТЗ.Колонки.Добавить(Имя,Новый ОписаниеТипов("Строка"));
		Исключение
			Сообщить(ОписаниеОшибки() + "Имя колонки:" + Имя);
			Возврат;
		КонецПопытки;
	КонецЦикла;
	
	Для Сч=1 По КолСтрок Цикл
		ТЗ.Добавить();
	КонецЦикла;
	
	Для Сч=0 По КолСтолбцов-1 Цикл
		ТЗ.ЗагрузитьКолонку(Данные[Сч], ИмяВФормате1С(Данные[Сч][0]));
	КонецЦикла;
	
	ТЗ.Удалить(ТЗ[0]);
	
	ВывестиРезультат(ТЗ);

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьИзФайлаMXLНаСервере(ФайлВХранилище)
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(ФайлВХранилище);
	ИмяТемпФайла = ПолучитьИмяВременногоФайла();
	ДвоичныеДанные.Записать(ИмяТемпФайла); 
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.Прочитать(ИмяТемпФайла);
	ПЗ = Новый ПостроительЗапроса;
	ПЗ.ИсточникДанных = Новый ОписаниеИсточникаДанных(ТабДок.Область());
	ПЗ.ДобавлениеПредставлений = ТипДобавленияПредставлений.НеДобавлять;
	ПЗ.ЗаполнитьНастройки();
	ПЗ.Выполнить();
	ТЗ =  ПЗ.Результат.Выгрузить();
	
	Файл = Новый Файл(ИмяТемпФайла);
	Если Файл.Существует() Тогда
		Попытка
			УдалитьФайлы(ИмяТемпФайла);
		Исключение
		КонецПопытки;
		
	КонецЕсли;
	
	ВывестиРезультат(ТЗ);
	
КонецПроцедуры

&НаСервере
Процедура ВывестиРезультат(ТЗРезультата)
	
	Колонки = ТЗРезультата.Колонки;
	ИмяСл = Колонки.Найти("СлужебныйРек__");
	Если ИмяСл <> Неопределено  Тогда
		Колонки.Удалить("СлужебныйРек__");
	КонецЕсли;

	ЭтаФорма[РекФормы].Очистить();
	
	МассивКУдалению = Новый Массив;
	Для Каждого Колонка Из РеквизитФормыВЗначение(РекФормы).Колонки Цикл
		
		Если Колонка.Имя = "СлужебныйРек__"  Тогда
			Продолжить;
		КонецЕсли;
		
		МассивКУдалению.Добавить(РекФормы+"."+Колонка.Имя);
		Если Колонка.ТипЗначения.СодержитТип(Тип("Число"))  Тогда
			МассивКУдалению.Добавить(РекФормы+"Итог"+Колонка.Имя);
		КонецЕсли;

		ЭлементФормы = Элементы.Найти(РекФормы + "_" + Колонка.Имя);
		Если ЭлементФормы <> Неопределено Тогда
			Элементы.Удалить(ЭлементФормы);
		КонецЕсли;
		
	КонецЦикла;
	
	МассивРеквизитов = Новый Массив;
	ЧисловыеКолонки = Новый Массив;
	Для Каждого КолонкаРез из Колонки Цикл
		ТекТип = КолонкаРез.ТипЗначения;
		Если КолонкаРез.ТипЗначения.СодержитТип(Тип("Число")) Тогда
			МассивРеквизитов.Добавить(Новый РеквизитФормы(РекФормы+"Итог"+КолонкаРез.Имя, Новый ОписаниеТипов("Число"), "")); 
			ЧисловыеКолонки.Добавить(КолонкаРез.Имя);
		КонецЕсли;
		МассивРеквизитов.Добавить(Новый РеквизитФормы(КолонкаРез.Имя, ТекТип, РекФормы));
	КонецЦикла;
	
	ИзменитьРеквизиты(МассивРеквизитов,МассивКУдалению);
	
	Для Каждого Колонка Из Колонки Цикл
 
        НовыйЭлемент = Элементы.Добавить(
            РекФормы + "_" + Колонка.Имя, Тип("ПолеФормы"), Элементы[РекФормы]
        );
        НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
        НовыйЭлемент.ПутьКДанным = РекФормы + "." + Колонка.Имя;
		
		Если Колонка.ТипЗначения.СодержитТип( Тип("Число")) Тогда
			НовыйЭлемент.ОтображатьВПодвале = Истина;
			НовыйЭлемент.ПутьКДаннымПодвала = РекФормы+"Итог" + КолонкаРез.Имя;
		КонецЕсли;
	КонецЦикла;
 
	ЭтаФорма[РекФормы].Загрузить(ТЗРезультата);
	Для Каждого ЧисловаяКолонка Из ЧисловыеКолонки Цикл
		ЭтаФорма[РекФормы+"Итог"+ЧисловаяКолонка] = ТЗРезультата.Итог(ЧисловаяКолонка);
	КонецЦикла;
	
	Элементы.Декорация0.Заголовок = "Всего "+Таблица0.Количество()+" строк";
	Элементы.Декорация1.Заголовок = "Всего "+Таблица1.Количество()+" строк";
	
КонецПроцедуры // ВывестиРезультат()

&НаСервере
Процедура ЗагрузитьИзТабДокНаСервере(ИмяТемпФайла)
	
	ТЗ = ЗначениеИзСтрокиВнутр(ИмяТемпФайла);
	ВывестиРезультат(ТЗ);
	
КонецПроцедуры

&НаСервере
Процедура ВставитьИзБуфераНаСервере(Текст)
	
	ТЗ = Новый ТаблицаЗначений;	
	МассивСтрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст,Символы.ВК);
	
	Стр = МассивСтрок[0];		
	Если НЕ ЗначениеЗаполнено(Стр) Тогда
		Возврат;
	КонецЕсли;
	
	МассивСтолбцов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Стр,Символы.Таб);
	Для НомерСтроки = 0 По МассивСтолбцов.Количество()-1 Цикл
		ЗаголовокКолонки = МассивСтолбцов[НомерСтроки];
		
		ИмяКолонки = ИмяВФормате1С(ЗаголовокКолонки);
		Если ИмяКолонки = "_" Тогда
			ИмяКолонки = ТЗ.Колонки[ТЗ.Колонки.Количество()-1].Имя  + 1;
		КонецЕсли;
		
		ТЗ.Колонки.Добавить(ИмяКолонки, Новый ОписаниеТипов("Строка"),ЗаголовокКолонки);
	КонецЦикла;
	
	Для НомерСтроки = 1 По МассивСтрок.Количество()-1 Цикл
		
		Стр= МассивСтрок[НомерСтроки];
		Стр= СтрЗаменить(Стр,Символы.ПС,"");
		
		Если ЗначениеЗаполнено(Стр) Тогда
			
			НоваяСтрока = ТЗ.Добавить();
			МассивЗначенийСтолбцов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Стр,Символы.Таб);
			Для НомерКол = 0 По МассивЗначенийСтолбцов.Количество()-1 Цикл
				НоваяСтрока[НомерКол] = МассивЗначенийСтолбцов[НомерКол];
			КонецЦикла;
			
		КонецЕсли;
	КонецЦикла;
	
	ВывестиРезультат(ТЗ);
	
КонецПроцедуры

&НаСервере
Функция СравнитьНаСервере()
	
	Таб0 = Таблица0.Выгрузить();
	Таб1 = Таблица1.Выгрузить();
	Таб0.Колонки.Удалить("СлужебныйРек__");
	Таб1.Колонки.Удалить("СлужебныйРек__");
		
	// Проверим поля
	Для каждого Колонка Из Таб0.Колонки Цикл
		Если Таб1.Колонки.Найти(Колонка.Имя) = Неопределено Тогда
			Сообщить("Колонка: " + Колонка.Имя + " не найдена в Таблице1");
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла; 
	Для каждого Колонка Из Таб1.Колонки Цикл
		Если Таб0.Колонки.Найти(Колонка.Имя) = Неопределено Тогда
			Сообщить("Колонка: " + Колонка.Имя + " не найдена в Таблице0");
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла; 
	
	Измерения = "";
	Для сч = 0 По Ключ Цикл
		Измерения = Измерения + ", " + Таб0.Колонки[сч].Имя;
	КонецЦикла; 
	Измерения = Сред(Измерения, 2);
	
	ТЗ = РазницаТаблицЗначений(Таб0, Таб1, Измерения);
	ВывестиРезультат(ТЗ);
	
	Возврат Истина;
КонецФункции

&НаСервере
Функция РазницаТаблицЗначений(Таблица0, Таблица1, Измерения)
    
    ВсеКолонки = "";
    Для Каждого Колонка Из Таблица0.Колонки Цикл 
        ВсеКолонки = ВсеКолонки + ", " + Колонка.Имя
    КонецЦикла;
    ВсеКолонки = Сред(ВсеКолонки, 2);
    
    Таблица1.Колонки.Добавить("Знак", Новый ОписаниеТипов("Число"));
    
    Таблица1.ЗаполнитьЗначения(1, "Знак");
    
    МассивСтрок = Таблица1.НайтиСтроки(Новый Структура("Знак", 1));
    
    БылаГраница = МассивСтрок.ВГраница();
    
    СталаГраница = БылаГраница + Таблица0.Количество();
    
    Если СталаГраница > БылаГраница Тогда
        МассивСтрок.Вставить(СталаГраница);    
        Для ё = 0 По БылаГраница Цикл МассивСтрок[СталаГраница - ё] = МассивСтрок[БылаГраница - ё] КонецЦикла;
        НоваяСтрока = Таблица1.Добавить();
        ТекущаяГраница = СталаГраница - БылаГраница - 1;
        Для ё = 0 По ТекущаяГраница Цикл МассивСтрок[ё] = НоваяСтрока КонецЦикла;
    КонецЕсли;     
    
    Таблица = Таблица1.Скопировать(МассивСтрок);
    
    Таблица1.Колонки.Удалить("Знак");
    
    //Таблица1.Удалить(БылаГраница + 1);
    
    Если СталаГраница > БылаГраница Тогда
        Для ё = 0 По Таблица0.Колонки.Количество() - 1 Цикл Таблица.ЗагрузитьКолонку(Таблица0.ВыгрузитьКолонку(ё), ё) КонецЦикла;
    КонецЕсли;
    
    Таблица.Колонки.Добавить("Счёт");
    Таблица.ЗаполнитьЗначения(1, "Счёт");
    
    Таблица.Свернуть(ВсеКолонки, "Знак, Счёт");
    
    Разница = Таблица.Скопировать(Новый Структура("Счёт", 1), ВсеКолонки + ", Знак");
    
    Разница.Сортировать(Измерения);
    
    Возврат Разница;
    
КонецФункции

&НаКлиенте
Процедура ВставитьИзБуфера(Команда)
	
	Текст = "";
	ОбъектБуфер = Новый COMОбъект("htmlfile");
	Буфер = ОбъектБуфер.ParentWindow.ClipboardData.Getdata("Text");
	Текст = СокрЛП(Буфер);
	
	ВставитьИзБуфераНаСервере(Текст);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВФайл(Команда)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДиалогВыбораФайла.Фильтр = "Файл данных (*.txt)|*.txt";
	ДиалогВыбораФайла.Расширение = "txt";
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.ИндексФильтра = 0;
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		ФайлВХранилище = ПолучитьТЗНаСервере();
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(ФайлВХранилище);
		ДвоичныеДанные.Записать(ДиалогВыбораФайла.ПолноеИмяФайла); 
		ПоказатьОповещениеПользователя("Данные сохранены",,"Выполнено сохранение данных в файл "+ДиалогВыбораФайла.ПолноеИмяФайла);
	КонецЕсли;  
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	
	ДиалогВыбораФайла.Фильтр = "Файл данных (txt xls mxl)|*.txt;*.xls;*.xlsx;*.mxl";
	ДиалогВыбораФайла.Расширение = "txt";
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.ИндексФильтра = 0;
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		Если Найти(ДиалогВыбораФайла.ПолноеИмяФайла,".txt") > 0 Тогда
			Файл = Новый ДвоичныеДанные(ДиалогВыбораФайла.ПолноеИмяФайла);
			Адрес = ПоместитьВоВременноеХранилище(Файл, ЭтаФорма.УникальныйИдентификатор);
			ОбработатьTXTНаСервере(Адрес);	
		ИначеЕсли Найти(ДиалогВыбораФайла.ПолноеИмяФайла,".mxl") > 0 Тогда
			Файл = Новый ДвоичныеДанные(ДиалогВыбораФайла.ПолноеИмяФайла);
			Адрес = ПоместитьВоВременноеХранилище(Файл, ЭтаФорма.УникальныйИдентификатор);
			ЗагрузитьИзФайлаMXLНаСервере(Адрес);
		Иначе
			
			Док = ПолучитьCOMОбъект(ДиалогВыбораФайла.ПолноеИмяФайла);
			
			ИмяКниги = ДиалогВыбораФайла.ПолноеИмяФайла;
			поз = Найти(ИмяКниги, "\");
			Пока поз > 0 Цикл
				ИмяКниги = Прав(ИмяКниги, СтрДлина(ИмяКниги) - поз );
				поз = Найти(ИмяКниги, "\");
			КонецЦикла;    
			
			ТекЛист = Док.Application.Workbooks(ИмяКниги).Worksheets(1);
			КолСтрок = ТекЛист.UsedRange.Rows.Count;
			КолСтолбцов = ТекЛист.UsedRange.Columns.Count;
			
			СомОбластьДанных = ТекЛист.Range(ТекЛист.Cells(1,1),ТекЛист.Cells(КолСтрок,КолСтолбцов));
			Данные = СомОбластьДанных.Value.Выгрузить();
			
			ЗагрузитьИзФайлаXLSНаСервере(Данные,КолСтрок,КолСтолбцов);
			
			Док = 0; 
		КонецЕсли;
		ЭтаФорма.ОбновитьОтображениеДанных();
		ПоказатьОповещениеПользователя("Данные загружены",,"Выполнена загрузка данных из файла "+ДиалогВыбораФайла.ПолноеИмяФайла);
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура УдалитьКолонкуНаСервере()
			
	ТЗ = РеквизитФормыВЗначение(РекФормы);
	ТЗ.Колонки.Удалить(ИмяЭлемента);
	ВывестиРезультат(ТЗ);
	
КонецПроцедуры

&НаСервере
Процедура ПереименоватьКолонкуНаСервере(НовоеИмя)
	
	ТЗ = РеквизитФормыВЗначение(РекФормы);
	ТЗ.Колонки[ИмяЭлемента].Имя = НовоеИмя;
	ВывестиРезультат(ТЗ);

КонецПроцедуры


&НаКлиенте
Процедура ВставитьИзБуфера0(Команда)
	РекФормы = "Таблица0";
	ВставитьИзБуфера(Команда);
КонецПроцедуры

&НаКлиенте
Процедура ВставитьИзБуфера1(Команда)
	РекФормы = "Таблица1";
	ВставитьИзБуфера(Команда);
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВФайл0(Команда)
	РекФормы = "Таблица0";
	СохранитьВФайл(Команда); 
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВФайл1(Команда)
	РекФормы = "Таблица1";
	СохранитьВФайл(Команда); 
КонецПроцедуры

&НаКлиенте
Процедура СохранитьВФайл2(Команда)
	РекФормы = "Таблица2";
	СохранитьВФайл(Команда); 
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла0(Команда)
	РекФормы = "Таблица0";
	ЗагрузитьИзФайла(Команда)
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла1(Команда)
	РекФормы = "Таблица1";
	ЗагрузитьИзФайла(Команда)
КонецПроцедуры

&НаКлиенте
Процедура Сравнить(Команда)
	РекФормы = "Таблица2";
	Если СравнитьНаСервере() Тогда
		Элементы.ГруппаЛево.Видимость = Ложь;
		Элементы.ГруппаПраво.Видимость = Ложь;
		Элементы.Ключ.Видимость = Ложь;
		Элементы.ГруппаРезультат.Видимость = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	Элементы.ГруппаЛево.Видимость = НЕ Элементы.ГруппаЛево.Видимость;
	Элементы.ГруппаПраво.Видимость = НЕ Элементы.ГруппаПраво.Видимость;
	Элементы.Ключ.Видимость = НЕ Элементы.Ключ.Видимость;
	Элементы.ГруппаРезультат.Видимость = НЕ Элементы.ГруппаРезультат.Видимость;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереименоватьКолонку(Команда)
	НовоеИмя = ИмяЭлемента;
	ПоказатьВводЗначения(Новый ОписаниеОповещения("ПереименоватьКолонкуЗавершение", ЭтаФорма, Новый Структура("НовоеИмя", НовоеИмя)), НовоеИмя,"Введите новое имя",Тип("Строка"));
КонецПроцедуры

&НаКлиенте
Процедура ПереименоватьКолонкуЗавершение(Значение, ДополнительныеПараметры) Экспорт
	
	НовоеИмя = ?(Значение = Неопределено, ДополнительныеПараметры.НовоеИмя, Значение);
	ПереименоватьКолонкуНаСервере(НовоеИмя);

КонецПроцедуры

&НаКлиенте
Процедура УдалитьКолонку(Команда)
	УдалитьКолонкуНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Таблица0ПриАктивизацииЯчейки(Элемент)
	
	Если Элементы.Таблица0.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;                              
	ИмяЭлемента = Элемент.ТекущийЭлемент.Имя;
	Если ИмяЭлемента = "РезультатТаблицаСлужебныйРек" Тогда
		Возврат;
	КонецЕсли;
	
	РекФормы = "Таблица0";
	ИмяЭлемента = Сред(ИмяЭлемента,СтрНайти(ИмяЭлемента,"_")+1);
	
КонецПроцедуры


&НаКлиенте
Процедура Таблица1ПриАктивизацииЯчейки(Элемент)
	
	Если Элементы.Таблица1.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;                              
	ИмяЭлемента = Элемент.ТекущийЭлемент.Имя;
	Если ИмяЭлемента = "РезультатТаблицаСлужебныйРек" Тогда
		Возврат;
	КонецЕсли;
	
	РекФормы = "Таблица1";
	ИмяЭлемента = Сред(ИмяЭлемента,СтрНайти(ИмяЭлемента,"_")+1);
	
КонецПроцедуры

&НаКлиенте
Процедура Таблица0ПриИзменении(Элемент)
	Элементы.Декорация0.Заголовок = "Всего "+Таблица0.Количество()+" строк";
	Элементы.Декорация1.Заголовок = "Всего "+Таблица1.Количество()+" строк";
КонецПроцедуры

&НаКлиенте
Процедура Таблица1ПриИзменении(Элемент)
	Элементы.Декорация0.Заголовок = "Всего "+Таблица0.Количество()+" строк";
	Элементы.Декорация1.Заголовок = "Всего "+Таблица1.Количество()+" строк";
КонецПроцедуры

&НаСервере
Процедура Сдвинуть(Направление)
	
	ТЗ = ЭтаФорма[РекФормы].Выгрузить();
	Колонки = ТЗ.Колонки;
	ИмяСл = Колонки.Найти("СлужебныйРек__");
	Если ИмяСл <> Неопределено  Тогда
		Колонки.Удалить("СлужебныйРек__");
	КонецЕсли;
	Колонки.Сдвинуть(ИмяЭлемента, Направление);
	ВывестиРезультат(ТЗ);	
	
КонецПроцедуры

&НаКлиенте
Процедура Правее(Команда)
	Сдвинуть(1);
КонецПроцедуры

&НаКлиенте
Процедура Левее(Команда)
	Сдвинуть(-1);
КонецПроцедуры



